import tp4.fabricaDeObjetosDePrueba.*
import tp4.usuario.*

describe "Cuándo es visible el contenido secreto" {
	const usuariosRestringidos = #{new Usuario(nombre = "Lola"), new Usuario(nombre = "Pepe")}
	
	method initialize() {
		fabrica.configurarContactos()
		usuariosRestringidos.forEach {usuario => 
			fabrica.creador().agregarContacto(usuario)
		}
	}
	
	method contactoNoRestringido() = fabrica.creador().contactos().find {contacto =>
		not usuariosRestringidos.contains(contacto)
	}
	method contactoRestringido() = usuariosRestringidos.anyOne()
	
	test "Una publicacion secreta la puede ver quien la publicó en cualquier fecha" {
		const contenido = fabrica.crearPublicacionSecreta(usuariosRestringidos)
		assert.that(contenido.esVisible(fabrica.creador(), fabrica.fechaLejanaAPublicacion()))
	}
	test "Una publicacion secreta la puede ver un contacto no restringido de quien la publicó en cualquier fecha" {
		const contenido = fabrica.crearPublicacionSecreta(usuariosRestringidos)
		assert.that(contenido.esVisible(self.contactoNoRestringido(), fabrica.fechaLejanaAPublicacion()))
	}
	test "Una publicacion secreta no la puede ver un contacto restringido de quien la publicó en cualquier fecha" {
		const contenido = fabrica.crearPublicacionSecreta(usuariosRestringidos)
		assert.notThat(contenido.esVisible(self.contactoRestringido(), fabrica.fechaLejanaAPublicacion()))
	}
	test "Una publicacion secreta no la puede ver un desconocido de quien la publicó en cualquier fecha" {
		const contenido = fabrica.crearPublicacionSecreta(usuariosRestringidos)
		assert.notThat(contenido.esVisible(fabrica.desconocido(), fabrica.fechaLejanaAPublicacion()))
	}
	test "Una historia secreta la puede ver quien la publicó en cualquier fecha" {
		const contenido = fabrica.crearHistoriaSecreta(usuariosRestringidos)
		assert.that(contenido.esVisible(fabrica.creador(), fabrica.fechaLejanaAPublicacion()))
	}
	test "Una historia secreta la puede ver un contacto no restringido de quien la publicó hasta un día después de su publicación" {
		const contenido = fabrica.crearHistoriaSecreta(usuariosRestringidos)
		assert.that(contenido.esVisible(self.contactoNoRestringido(), fabrica.diaSiguienteAPublicacion()))
	}
	test "Una historia secreta NO la puede ver un contacto no restringido de quien la publicó pasado más de un día después de su publicación" {
		const contenido = fabrica.crearHistoriaSecreta(usuariosRestringidos)
		assert.notThat(contenido.esVisible(self.contactoNoRestringido(), fabrica.diaSiguienteAPublicacion().plusDays(1)))
	}
	test "Una historia secreta NO la puede ver un contacto restringido de quien la publicó hasta un día después de su publicación" {
		const contenido = fabrica.crearHistoriaSecreta(usuariosRestringidos)
		assert.notThat(contenido.esVisible(self.contactoRestringido(), fabrica.diaSiguienteAPublicacion()))
	}
	test "Una historia secreta NO la puede ver un desconocido de quien la publicó hasta un día después de su publicación" {
		const contenido = fabrica.crearHistoriaSecreta(usuariosRestringidos)
		assert.notThat(contenido.esVisible(fabrica.desconocido(), fabrica.diaSiguienteAPublicacion()))
	}
}